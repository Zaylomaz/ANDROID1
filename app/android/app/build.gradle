def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 34

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.rempc.app"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "release-type"

    productFlavors {
        dev {
            dimension "release-type"
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
            buildConfigField "String", "API_URL", "\"https://dev.rempc-crm.com/\""
            buildConfigField "String", "PROXY_URL", "\"https://dev.rempc-crm.org.ua/\""
        }

        prod {
            dimension "release-type"
            applicationId "com.rempc.app"
            buildConfigField "String", "API_URL", "\"https://rempc-crm.com/\""
            buildConfigField "String", "PROXY_URL", "\"https://rempc-crm.org.ua/\""
        }

        prodapk {
            dimension "release-type"
            applicationId "com.rempc.app"
            buildConfigField "String", "API_URL", "\"https://rempc-crm.com/\""
            buildConfigField "String", "PROXY_URL", "\"https://rempc-crm.org.ua/\""
        }
    }

    lint {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }

    namespace 'com.rempc.app'

}

flutter {
    source '../..'
}

dependencies {
    implementation(platform("com.google.firebase:firebase-bom:32.2.0"))
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation("com.google.firebase:firebase-analytics-ktx")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.greenrobot:eventbus:3.3.1"
    implementation("com.squareup.okhttp3:okhttp:4.9.2")
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.2"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.google.android.gms:play-services-awareness:19.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'androidx.window:window:1.1.0'
    implementation 'androidx.window:window-java:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.lifecycle:lifecycle-common:2.6.2'
    implementation 'com.onesignal:OneSignal:[4.0.0, 4.99.99]'
    implementation "com.google.guava:guava:31.0.1-android"
}
